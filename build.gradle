plugins {
    id "com.avast.gradle.docker-compose" version "0.6.6"
}

group 'com.codebrig.phenomena'
version '0.2.3'

apply plugin: 'groovy'

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
    maven {
        url 'https://repo.grakn.ai/repository/maven/'
    }
}

dependencies {
    testImplementation group: 'junit', name: 'junit', version: '4.12'
    implementation group: 'org.slf4j', name: 'slf4j-log4j12', version: '1.7.26'
    implementation 'org.codehaus.groovy:groovy-all:2.4.15'
    implementation 'com.github.javaparser:javaparser-symbol-solver-core:3.7.0'
    implementation group: 'commons-io', name: 'commons-io', version: '2.5'
    implementation group: 'io.grakn.client', name: 'grakn-client', version: "1.6.2"
    implementation group: 'io.graql', name: 'graql-lang', version: "1.0.5"
    implementation 'org.bblfsh:bblfsh-client:1.11.0'
    implementation 'com.github.codebrig:arthur:v0.4.0-alpha'
}

test {
    dependsOn "composeUp"
    finalizedBy "composeDown"
}

dockerCompose {
    useComposeFiles = ['./docker-compose.yml']
    dockerComposeWorkingDirectory = './docker/'
    captureContainersOutput = true
}

task createProperties(dependsOn: processResources) {
    doLast {
        new File("$buildDir/resources/main/phenomena_build.properties").withWriter { w ->
            Properties p = new Properties()
            p['version'] = project.version.toString()
            p.store w, null
        }
    }
}

classes {
    dependsOn createProperties
}
